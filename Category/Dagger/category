    let kArrow = ./../../Function/Kind

in  let kProduct = ./../../Tuple/Kind

in    λ(object : Kind)
    → λ(arrow : kArrow (kProduct object object) Type)
    → λ(category : ./../Type object arrow)
    →     let opArrow = ./../Op/Type object arrow
      
      in  let opCategory = ./../Op/category object arrow category
      
      in  let Dagger = ./Type object arrow
      
      in    { identity =
                  λ(a : object)
                → { _1 = category.identity a, _2 = opCategory.identity a }
            , op =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → Dagger { _1 = z, _2 = a._2 }
                          → Dagger { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → { _1 =
                      category.op
                      a
                      (   λ(r : Type)
                        → λ ( arrowsOut
                            :   ∀(z : object)
                              → arrow { _1 = z, _2 = a._2 }
                              → arrow { _1 = a._1, _2 = z }
                              → r
                            )
                        → p
                          r
                          (   λ(z : object)
                            → λ(f : Dagger { _1 = z, _2 = a._2 })
                            → λ(g : Dagger { _1 = a._1, _2 = z })
                            → arrowsOut z f._1 g._1
                          )
                      )
                  , _2 =
                      opCategory.op
                      a
                      (   λ(r : Type)
                        → λ ( arrowsOut
                            :   ∀(z : object)
                              → opArrow { _1 = z, _2 = a._2 }
                              → opArrow { _1 = a._1, _2 = z }
                              → r
                            )
                        → p
                          r
                          (   λ(z : object)
                            → λ(f : Dagger { _1 = z, _2 = a._2 })
                            → λ(g : Dagger { _1 = a._1, _2 = z })
                            → arrowsOut z f._2 g._2
                          )
                      )
                  }
            }
          : ./../Type object Dagger
